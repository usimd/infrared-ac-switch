* SPICE model bridge rectifier ***GBI25A***
* Use with symbol file ***gbi25a.asy***
*
* (c) Diotec Semiconductor AG
* www.diotec.com
* 2017-12-21
*
*********************************************************
* This model is for simulation purposes only. It does   *
* not replace reviewing of the data sheet nor real life *
* testing of the part inside the application.           *
*********************************************************
*
.subckt GBI25A ~1 ~2 + -  params: Vrrm=50 Vrsm=50 Ir=5u Irsm=5u Vf=1.1 Ifav=12.5 Rs=0.002 trr=1500n Cj=85p Eg=1.11 Xti=3

* Above values are an example for the ***GBI25A***. Using the
* above symbol file allows for direct insertion of other values
* according to these data sheet parameters:
*
* Vrrm    	Repetitive peak reverse voltage
* Ir		Leakage current
* Vrsm    	Surge peak reverse voltage / Reverse avalanche breakdown voltage
* Irsm		Defined at avalanche diodes, otherwise set Irsm = Ir
* Vf		Forward voltage
* Ifav		Test current for Vf, usually equal to average forward rectified current
* trr		Reverse recovery time; for Schottky, set trr=5n
* Cj		Junction capacitance at 4V
*
* Activation energy: Eg=1.11 for Si (pn) rectifier, Eg=.69 for Schottky (metal barrier) rectifier
* Series resistance: Rs=(Vf@3*Ifav - Vf@Ifav)/(3*Ifav - Ifav) from data sheet curve
* Sat.-current temp. exp: Xti=3 for Si (pn) rectifier, Xti=2 for Schottky (metal barrier) rectifier

D1 ~1 + Bridge
D2 ~2 + Bridge
D3 - ~1 Bridge
D4 - ~2 Bridge

.model Bridge D(Is={Ir/20} Bv={Vrsm*1.05} Ibv={Ir} Vpk={Vrrm} N={.8*Vf/25m/(ln(Ifav)-ln(Ir/20))} Rs={Rs} Eg={Eg} Xti={Xti} Iave={Ifav} Cjo={Cj*2} M=.33 Tt={trr} Vp=.5 mfg=Diotec)

.ends
